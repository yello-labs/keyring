plugins {
    id 'java'
}

group 'org.yellolab'
version '0.1.0'

repositories {
    mavenCentral()
}

sourceSets.main.resources {
    srcDir "src/main/resources"
    srcDir "keyring-jni/target/release"
    include "libkeyring_jni**"
}

dependencies {
    testImplementation 'org.testng:testng:6.10'
}

task cargoBuild(type: Exec) {
    workingDir './keyring-jni/'
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'cmd', '/c', 'cargo', 'build', '--release'
    } else {
        commandLine 'cargo', 'build', '--release'
    }
}

task getLibFromRust(type: Copy) {
    dependsOn cargoBuild
    from('keyring-jni/target/release/') {
        include 'libkeyring_jni*'
        exclude '*.d'
        rename {
            'libkeyring_jni'
        }
    }
    into 'src/main/resources/'
}
test {
    useTestNG()
    failFast = false
}
compileJava {
    dependsOn getLibFromRust
}